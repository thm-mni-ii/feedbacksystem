plugins {
    id 'idea'
    id 'distribution'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

apply plugin: 'application'
apply plugin: 'scala'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.scala-lang:scala-library:2.13.1'
}

subprojects { subproject ->
    group 'de.thm.ii'
    version '1.0-SNAPSHOT'
}

task dist(dependsOn: ['ws:installDist', "master-runner:installDist", "web-gui:installDist", "web-gui:copyWebToWS"]) {
    doLast({
        installDist
    })
}

distributions {
    main {
        distributionBaseName = project.name
        contents {
            project.subprojects.each { sub ->
                switch (sub.name) {
                    case 'web-gui':
                        into('ws/http') {
                            from "${sub.buildDir}/install/${sub.name}"
                            include '*/**'
                        }
                        break;
                    case 'ws':
                        into('') {
                            from "${sub.buildDir}/install/"
                            include '*/**'
                        }
                        into(sub.name) {
                            from "${sub.projectDir}"
                            include 'Dockerfile'
                        }
                        break;
                }
            }
        }
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = ['ws:test', 'master-runner:test']
    additionalSourceDirs.setFrom files('ws/src/main/scala', 'master-runner/src/main/scala')
    sourceDirectories.setFrom files('ws/src/main/scala', 'master-runner/src/main/scala')
    classDirectories.setFrom files('ws/build/classes/scala/main', 'master-runner/build/classes/scala/main')
    executionData.setFrom files('ws/build/jacoco/test.exec', 'master-runner/build/jacoco/test.exec')

    reports {
        xml.enabled true
        html.enabled true
    }
}

coveralls {
    sourceDirs = ['ws/src/main/scala', 'master-runner/src/main/scala']
    jacocoReportPath "build/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}
