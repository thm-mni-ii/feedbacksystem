import org.codehaus.groovy.runtime.GStringImpl

plugins {
    id 'idea'
    id 'distribution'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.0'
}

apply plugin: 'application'
apply plugin: 'scala'

repositories {
    mavenCentral()
}

ext {
    scalaStyleConfig = "$rootDir/scalastyle_config.xml"
}

subprojects { subproject ->
    group 'de.thm.ii'
    version '1.0-SNAPSHOT'
}

task dist(dependsOn: ["fbs-runner.checker:installDist", "distApi", "distWeb"]) {}

task distApi(dependsOn: ['fbs-core.api:installDist']) {}
task distWeb(dependsOn: ["fbs-core.web:installDist", "fbs-core.web:copyWebToWS"])
distApi.shouldRunAfter distWeb

distributions {
    main {
        distributionBaseName = project.name
        contents {
            project.subprojects.each { sub ->
                switch (sub.name) {
                    case 'fbs-core.web':
                        into('modules/fbs-core/api/http') {
                            from "${sub.buildDir}/install/${sub.name}"
                            include '*/**'
                        }
                        break
                    case 'fbs-core.api':
                        into('') {
                            from "${sub.buildDir}/install/"
                            include '*/**'
                        }
                        into(sub.name) {
                            from "${sub.projectDir}"
                            include 'Dockerfile'
                        }
                        break
                }
            }
        }
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = ['fbs-core.api:test', 'fbs-runner.checker:test']
    additionalSourceDirs.setFrom files('modules/fbs-core/api/src/main/scala', 'modules/fbs-runner/checker/src/main/scala')
    sourceDirectories.setFrom files('modules/fbs-core/api/src/main/scala', 'modules/fbs-runner/checker/src/main/scala')
    classDirectories.setFrom files('modules/fbs-core/api/build/classes/scala/main', 'modules/fbs-runner/checker/build/classes/scala/main')
    executionData.setFrom files('modules/fbs-core/api/build/jacoco/test.exec', 'modules/fbs-runner/checker/build/jacoco/test.exec')

    reports {
        xml.enabled true
        html.enabled true
    }
}

coveralls {
    sourceDirs = ['modules/fbs-core/api/src/main/scala', 'modules/fbs-runner/checker/src/main/scala']
    jacocoReportPath "build/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

dependencies {
    compileOnly 'org.scala-lang:scala-library:2.13.1'
}

